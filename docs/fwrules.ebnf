rule        ::= 'FROM' target_list 'TO' target_list action protocol
target_list ::= 'ANY'
              | 'ALL VMS'
              | '(' target ( 'OR' target )* ')'
              | target
target      ::= 'IP' address
              | 'SUBNET' cidr
              | 'TAG' tag_string
              | 'TAG' tag_string '=' tag_value
              | 'VM' uuid
action      ::= 'BLOCK'
              | 'ALLOW'
protocol    ::= 'TCP' port_list
              | 'UDP' port_list
              | 'ICMP' type_list
              | 'ICMP6' type_list
port_list   ::= '(' port ( 'AND' port )* ')'
              | port
              | 'PORTS' portrange ( ',' portrange )*
              | 'PORT ALL'
portnumber  ::= '1 - 65535'
port        ::= 'PORT' portnumber
portrange   ::= portnumber
              | portnumber '-' portnumber
type_list   ::= '(' type ( 'AND' type )* ')'
              | type
type        ::= 'TYPE' '0 - 255' ( 'CODE' '0 - 255' )?
              | 'TYPE ALL'
